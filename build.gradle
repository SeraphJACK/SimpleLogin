buildscript {
    repositories {
        jcenter() {
            url = 'https://jcenter.bintray.com/'
        }
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            url 'https://repository.jboss.org/nexus/content/groups/public'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

allprojects {
    repositories {
        jcenter() {
            url = 'https://jcenter.bintray.com/'
        }
        maven {
            url 'https://repository.jboss.org/nexus/content/groups/public'
        }
        mavenCentral()
    }
}

apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

version = "1.2.3-beta"
group = "top.seraphjack"
archivesBaseName = "simplelogin"

if (System.getenv().BUILD_NUMBER != null) {
    version += "+build${System.getenv().BUILD_NUMBER}"
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"

    mappings = "${mapping_version}"

    replace "@VERSION_INJECT@", project.version
}

configurations {
    compile.extendsFrom shadow
}

dependencies {
    testCompile "junit:junit:4.12"
    shadow "org.mindrot:jbcrypt:0.4"
    shadow "org.mariadb.jdbc:mariadb-java-client:2.6.1"
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier = ""

    exclude "org/mariadb/**"
}
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
task mysqlJar(type: ShadowJar) {
    configurations = [project.configurations.shadow]
    classifier = 'mysql'
    from sourceSets.main.output
}

jar {
    classifier = "nolib"
    exclude "LICENSE-jBCrypt"
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
    mysqlJar { mappingType = 'SEARGE' }
}

tasks.reobfShadowJar.mustRunAfter shadowJar
tasks.reobfMysqlJar.mustRunAfter mysqlJar

tasks.sourceJar.exclude("LICENSE-jBCrypt")

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

artifacts {
    archives mysqlJar
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
